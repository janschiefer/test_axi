///AXI4 bus package prototype
pub proto package axi4_prototype {
    const ADDRESS_WIDTH   : u32;
    const DATA_WIDTH_BYTES: u32;
    const NUM_BURSTS_LEN  : u32;
    const LOCK_LEN        : u32;
    const ID_LENGTH       : u32;
    const AWUSER_LENGTH   : u32;
    const WUSER_LENGTH    : u32;
    const BUSER_LENGTH    : u32;
    const ARUSER_LENGTH   : u32;
    const RUSER_LENGTH    : u32;

    type addr_t      ;
    type data_t      ;
    type strb_t      ;
    type num_bursts_t;
    type lock_t      ;
    type id_t        ;
    type awuser_t    ;
    type wuser_t     ;
    type buser_t     ;
    type aruser_t    ;
    type ruser_t     ;

    type size_t ;
    type burst_t;
    type cache_t;
    type prot_t ;
    type bresp_t;
    type rresp_t;
}

///AXI3 bus package prototype
pub proto package axi3_prototype {
    const ADDRESS_WIDTH   : u32;
    const DATA_WIDTH_BYTES: u32;
    const NUM_BURSTS_LEN  : u32;
    const LOCK_LEN        : u32;
    const ID_LENGTH       : u32;

    type addr_t      ;
    type data_t      ;
    type strb_t      ;
    type num_bursts_t;
    type lock_t      ;
    type id_t        ;

    type size_t ;
    type burst_t;
    type cache_t;
    type prot_t ;
    type bresp_t;
    type rresp_t;
}

///AXI4-Lite bus package prototype
pub proto package axi4_lite_prototype {
    const ADDRESS_WIDTH   : u32;
    const DATA_WIDTH_BYTES: u32;
    type addr_t          ;
    type data_t          ;
    type strb_t          ;
    type prot_t          ;
    type id_t            ;

    type bresp_t;
    type rresp_t;
}

pub package axi4_config {

    enum axsize_variants: logic<3> {
        bytes_per_transfer_1 = 3'b000,
        bytes_per_transfer_2 = 3'b001,
        bytes_per_transfer_4 = 3'b010,
        bytes_per_transfer_8 = 3'b011,
        bytes_per_transfer_16 = 3'b100,
        bytes_per_transfer_32 = 3'b101,
        bytes_per_transfer_64 = 3'b110,
        bytes_per_transfer_128 = 3'b111,
    }

    enum axburst_variants: logic<2> {
        fixed_burst = 2'b00,
        incrementing_burst = 2'b01,
        wrapping_burst = 2'b10,
    }

    const awcache_bufferable    : logic<4> = 4'b0001;
    const awcache_modifiable    : logic<4> = 4'b0010;
    const awcache_other_allocate: logic<4> = 4'b0100;
    const awcache_allocate      : logic<4> = 4'b1000;

    const arcache_bufferable    : logic<4> = 4'b0001;
    const arcache_modifiable    : logic<4> = 4'b0010;
    const arcache_allocate      : logic<4> = 4'b0100;
    const arcache_other_allocate: logic<4> = 4'b1000;

    const axprot_privileged        : logic<3> = 3'b001;
    const axprot_non_secure        : logic<3> = 3'b010;
    const axprot_instruction_access: logic<3> = 3'b100;

    enum bresp_variants: logic<2> {
        okay = 2'b00,
        exokay = 2'b01,
        slverr = 2'b10,
        decerr = 2'b11,
    }

    enum rresp_variants: logic<2> {
        okay = 2'b00,
        exokay = 2'b01,
        slverr = 2'b10,
        decerr = 2'b11,
    }

}

///AXI4 bus package
pub package axi4_pkg::<ADDR_W: u32, DATA_W_BYTES: u32, ID_W: u32, AWUSER_W: u32, WUSER_W: u32, BUSER_W: u32, ARUSER_W: u32, RUSER_W: u32> for axi4_prototype {
    const ADDRESS_WIDTH   : u32 = ADDR_W;
    const DATA_WIDTH_BYTES: u32 = DATA_W_BYTES;
    const NUM_BURSTS_LEN  : u32 = 8;
    const LOCK_LEN        : u32 = 1;
    const ID_LENGTH       : u32 = ID_W;
    const AWUSER_LENGTH   : u32 = AWUSER_W;
    const WUSER_LENGTH    : u32 = WUSER_W;
    const BUSER_LENGTH    : u32 = BUSER_W;
    const ARUSER_LENGTH   : u32 = ARUSER_W;
    const RUSER_LENGTH    : u32 = RUSER_W;

    type addr_t       = logic<ADDRESS_WIDTH>         ;
    type data_t       = logic<(DATA_WIDTH_BYTES * 8)>;
    type strb_t       = logic<DATA_WIDTH_BYTES>      ;
    type num_bursts_t = logic<NUM_BURSTS_LEN>        ;
    type lock_t       = logic<LOCK_LEN>              ;
    type id_t         = logic<ID_LENGTH>             ;
    type awuser_t     = logic<AWUSER_LENGTH>         ;
    type wuser_t      = logic<WUSER_LENGTH>          ;
    type buser_t      = logic<BUSER_LENGTH>          ;
    type aruser_t     = logic<ARUSER_LENGTH>         ;
    type ruser_t      = logic<RUSER_LENGTH>          ;

    type size_t  = axi4_config::axsize_variants    ;
    type burst_t = axi4_config::axburst_variants   ;
    type cache_t = logic                        <4>;
    type prot_t  = logic                        <3>;
    type bresp_t = axi4_config::bresp_variants     ;
    type rresp_t = axi4_config::rresp_variants     ;

}

pub package axi3_config {

    enum axsize_variants: logic<3> {
        bytes_per_transfer_1 = 3'b000,
        bytes_per_transfer_2 = 3'b001,
        bytes_per_transfer_4 = 3'b010,
        bytes_per_transfer_8 = 3'b011,
        bytes_per_transfer_16 = 3'b100,
        bytes_per_transfer_32 = 3'b101,
        bytes_per_transfer_64 = 3'b110,
        bytes_per_transfer_128 = 3'b111,
    }

    enum axburst_variants: logic<2> {
        fixed_burst = 2'b00,
        incrementing_burst = 2'b01,
        wrapping_burst = 2'b10,
    }

    const axcache_bufferable    : logic<4> = 4'b0001;
    const axcache_cacheable     : logic<4> = 4'b0010;
    const axcache_read_allocate : logic<4> = 4'b0100;
    const axcache_write_allocate: logic<4> = 4'b1000;

    const axprot_privileged        : logic<3> = 3'b001;
    const axprot_non_secure        : logic<3> = 3'b010;
    const axprot_instruction_access: logic<3> = 3'b100;

    enum bresp_variants: logic<2> {
        okay = 2'b00,
        exokay = 2'b01,
        slverr = 2'b10,
        decerr = 2'b11,
    }

    enum rresp_variants: logic<2> {
        okay = 2'b00,
        exokay = 2'b01,
        slverr = 2'b10,
        decerr = 2'b11,
    }

}

///AXI3 bus package
pub package axi3_pkg::<ADDR_W: u32, DATA_W_BYTES: u32, ID_W: u32> for axi3_prototype {
    const ADDRESS_WIDTH   : u32 = ADDR_W;
    const DATA_WIDTH_BYTES: u32 = DATA_W_BYTES;
    const NUM_BURSTS_LEN  : u32 = 4;
    const LOCK_LEN        : u32 = 2;
    const ID_LENGTH       : u32 = ID_W;

    type addr_t       = logic<ADDRESS_WIDTH>         ;
    type data_t       = logic<(DATA_WIDTH_BYTES * 8)>;
    type strb_t       = logic<DATA_WIDTH_BYTES>      ;
    type num_bursts_t = logic<NUM_BURSTS_LEN>        ;
    type lock_t       = logic<LOCK_LEN>              ;
    type id_t         = logic<ID_LENGTH>             ;

    type size_t  = axi3_config::axsize_variants    ;
    type burst_t = axi3_config::axburst_variants   ;
    type cache_t = logic                        <4>;
    type prot_t  = logic                        <3>;
    type bresp_t = axi3_config::bresp_variants     ;
    type rresp_t = axi3_config::rresp_variants     ;
}

pub package axi4_lite_config {

    const axprot_privileged        : logic<3> = 3'b001;
    const axprot_non_secure        : logic<3> = 3'b010;
    const axprot_instruction_access: logic<3> = 3'b100;

    enum bresp_variants: logic<2> {
        okay = 2'b00,
        exokay = 2'b01,
        slverr = 2'b10,
        decerr = 2'b11,
    }

    enum rresp_variants: logic<2> {
        okay = 2'b00,
        exokay = 2'b01,
        slverr = 2'b10,
        decerr = 2'b11,
    }

}

///AXI4-Lite bus package
pub package axi4_lite_pkg::<ADDR_W: u32, DATA_W_BYTES: u32, ID_W: u32> for axi4_lite_prototype {
    const ADDRESS_WIDTH   : u32 = ADDR_W;
    const DATA_WIDTH_BYTES: u32 = DATA_W_BYTES;
    const ID_LENGTH       : u32 = ID_W;

    type addr_t = logic<ADDRESS_WIDTH>         ;
    type data_t = logic<(DATA_WIDTH_BYTES * 8)>;
    type strb_t = logic<DATA_WIDTH_BYTES>      ;

    type id_t = logic<ID_LENGTH>;

    type prot_t  = logic                           <3>;
    type bresp_t = axi4_lite_config::bresp_variants   ;
    type rresp_t = axi4_lite_config::rresp_variants   ;

}

///### AXI4 bus interface
///
///#### Modports:
///* master - AXI Master IP
///
///* slave - AXI slave IP
///
///* monitor - Debugging - All signals are declared as input
///
///* write_master - Reduced signal count for master IP that only writes to slave
///
///* read_master - Reduced signal count for master IP that only reads from slave
///
///* write_slave - Reduced signal count for slave IP that only receives writes from master
///
///* read_slave - Reduced signal count for slave IP that only replies to read requests from master
///
///#### Convenience functions:
///* awaddr_ack() = awready && awvalid
///
///* wdata_ack() = wready && wvalid
///
///* bresp_ack() = bready && bvalid
///
///* araddr_ack() = arready && arvalid
///
///* rdata_ack() = rready && rvalid
///
///#### Instantiation:
///```
///inst a: axi4_if::< axi4_pkg::< ADDR_W, DATA_W_BYTES, ID_W,
///                               AWUSER_W, WUSER_W, BUSER_W,
///                               ARUSER_W, RUSER_W > >;
///```
///#### Usage in module definition with modport:
///```
///module my_axi4_slave ( aclk: input clock_posedge,
///                       aresetn: input reset_sync_low,
///                       axi: modport axi4_if::<axi4_pkg::<32, 4, 8,
///                                                         8, 8, 8,
///                                                         8, 8>>::slave ) {
///
///}
///```
pub interface axi4_if::<PKG: axi4_prototype> {

    import PKG::*;

    //Write address channel
    var awvalid : logic          ;
    var awready : logic          ;
    var awaddr  : addr_t         ;
    var awsize  : size_t         ;
    var awburst : burst_t        ;
    var awcache : cache_t        ;
    var awprot  : prot_t         ;
    var awid    : id_t           ;
    var awlen   : num_bursts_t   ; //AXI3: 4 bits, AXI4: 8 bits
    var awlock  : lock_t         ; //AXI3: 2 bits, AXI4: 1 bit
    var awqos   : logic       <4>; //Only AXI4
    var awregion: logic       <4>; //Only AXI4
    var awuser  : awuser_t       ; //Only AXI4

    //Write data channel
    var wvalid: logic  ;
    var wready: logic  ;
    var wlast : logic  ;
    var wdata : data_t ;
    var wstrb : strb_t ;
    var wuser : wuser_t; //Only AXI4

    //Write response channel
    var bvalid: logic  ;
    var bready: logic  ;
    var bresp : bresp_t;
    var bid   : id_t   ;
    var buser : buser_t; //Only AXI4

    //Read address channel
    var arvalid : logic          ;
    var arready : logic          ;
    var araddr  : addr_t         ;
    var arsize  : size_t         ;
    var arburst : burst_t        ;
    var arcache : cache_t        ;
    var arprot  : prot_t         ;
    var arid    : id_t           ;
    var arlen   : num_bursts_t   ; //AXI3: 4 bits, AXI4: 8 bits
    var arlock  : lock_t         ; //AXI3: 2 bits, AXI4: 1 bit
    var arqos   : logic       <4>; //Only AXI4
    var arregion: logic       <4>; //Only AXI4
    var aruser  : aruser_t       ; //Only AXI4

    //Read data channel
    var rvalid: logic  ;
    var rready: logic  ;
    var rlast : logic  ;
    var rdata : data_t ;
    var rresp : rresp_t;
    var rid   : id_t   ;
    var ruser : ruser_t; //Only AXI4

    function awaddr_ack () -> logic {
        return awready && awvalid;
    }

    function wdata_ack () -> logic {
        return wready && wvalid;
    }

    function bresp_ack () -> logic {
        return bready && bvalid;
    }

    function araddr_ack () -> logic {
        return arready && arvalid;
    }

    function rdata_ack () -> logic {
        return rready && rvalid;
    }

    modport master {

        awvalid : output,
        awready : input ,
        awaddr  : output,
        awsize  : output,
        awburst : output,
        awcache : output,
        awprot  : output,
        awid    : output,
        awlen   : output,
        awlock  : output,
        awqos   : output,
        awregion: output,
        awuser  : output,

        wvalid: output,
        wready: input ,
        wlast : output,
        wdata : output,
        wstrb : output,
        wuser : output,

        bvalid: input ,
        bready: output,
        bresp : input ,
        bid   : input ,
        buser : input ,

        arvalid : output,
        arready : input ,
        araddr  : output,
        arsize  : output,
        arburst : output,
        arcache : output,
        arprot  : output,
        arid    : output,
        arlen   : output,
        arlock  : output,
        arqos   : output,
        arregion: output,
        aruser  : output,

        rvalid: input ,
        rready: output,
        rlast : input ,
        rdata : input ,
        rresp : input ,
        rid   : input ,
        ruser : input ,

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

    }

    modport slave {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

        ..converse(master)
    }

    modport monitor {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

        ..input
    }

    modport write_master {
        awvalid : output,
        awready : input ,
        awaddr  : output,
        awsize  : output,
        awburst : output,
        awcache : output,
        awprot  : output,
        awid    : output,
        awlen   : output,
        awlock  : output,
        awqos   : output,
        awregion: output,
        awuser  : output,

        wvalid: output,
        wready: input ,
        wlast : output,
        wdata : output,
        wstrb : output,
        wuser : output,

        bvalid: input ,
        bready: output,
        bresp : input ,
        bid   : input ,
        buser : input ,

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
    }

    modport read_master {

        arvalid : output,
        arready : input ,
        araddr  : output,
        arsize  : output,
        arburst : output,
        arcache : output,
        arprot  : output,
        arid    : output,
        arlen   : output,
        arlock  : output,
        arqos   : output,
        arregion: output,
        aruser  : output,

        rvalid: input ,
        rready: output,
        rlast : input ,
        rdata : input ,
        rresp : input ,
        rid   : input ,
        ruser : input ,

        araddr_ack: import,
        rdata_ack : import,

    }

    modport write_slave {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,

        ..converse(write_master)
    }

    modport read_slave {

        araddr_ack: import,
        rdata_ack : import,

        ..converse(read_master)
    }
}

///### AXI3 bus interface
///
///#### Modports:
///* master - AXI Master IP
///
///* slave - AXI slave IP
///
///* monitor - Debugging - All signals are declared as input
///
///* write_master - Reduced signal count for master IP that only writes to slave
///
///* read_master - Reduced signal count for master IP that only reads from slave
///
///* write_slave - Reduced signal count for slave IP that only receives writes from master
///
///* read_slave - Reduced signal count for slave IP that only replies to read requests from master
///
///#### Convenience functions:
///* awaddr_ack() = awready && awvalid
///
///* wdata_ack() = wready && wvalid
///
///* bresp_ack() = bready && bvalid
///
///* araddr_ack() = arready && arvalid
///
///* rdata_ack() = rready && rvalid
///
///#### Instantiation:
///```
///inst b: axi3_if::< axi3_pkg::< ADDR_W, DATA_W_BYTES, ID_W > >;
///```
///#### Usage in module definition with modport:
///```
///module my_axi3_slave ( aclk: input clock_posedge,
///                       aresetn: input reset_sync_low,
///                       axi: modport axi3_if::<axi3_pkg::<32, 4, 8>>::slave ) {
///
///}
///```
pub interface axi3_if::<PKG: axi3_prototype> {

    import PKG::*;

    //Write address channel
    var awvalid: logic       ;
    var awready: logic       ;
    var awaddr : addr_t      ;
    var awsize : size_t      ;
    var awburst: burst_t     ;
    var awcache: cache_t     ;
    var awprot : prot_t      ;
    var awid   : id_t        ;
    var awlen  : num_bursts_t; //AXI3: 4 bits, AXI4: 8 bits
    var awlock : lock_t      ; //AXI3: 2 bits, AXI4: 1 bit

    //Write data channel
    var wvalid: logic ;
    var wready: logic ;
    var wlast : logic ;
    var wdata : data_t;
    var wstrb : strb_t;
    var wid   : id_t  ; //Only AXI3

    //Write response channel
    var bvalid: logic  ;
    var bready: logic  ;
    var bresp : bresp_t;
    var bid   : id_t   ;

    //Read address channel
    var arvalid: logic       ;
    var arready: logic       ;
    var araddr : addr_t      ;
    var arsize : size_t      ;
    var arburst: burst_t     ;
    var arcache: cache_t     ;
    var arprot : prot_t      ;
    var arid   : id_t        ;
    var arlen  : num_bursts_t; //AXI3: 4 bits, AXI4: 8 bits
    var arlock : lock_t      ; //AXI3: 2 bits, AXI4: 1 bit

    //Read data channel
    var rvalid: logic  ;
    var rready: logic  ;
    var rlast : logic  ;
    var rdata : data_t ;
    var rresp : rresp_t;
    var rid   : id_t   ;

    function awaddr_ack () -> logic {
        return awready && awvalid;
    }

    function wdata_ack () -> logic {
        return wready && wvalid;
    }

    function bresp_ack () -> logic {
        return bready && bvalid;
    }

    function araddr_ack () -> logic {
        return arready && arvalid;
    }

    function rdata_ack () -> logic {
        return rready && rvalid;
    }

    modport master {

        awvalid: output,
        awready: input ,
        awaddr : output,
        awsize : output,
        awburst: output,
        awcache: output,
        awprot : output,
        awid   : output,
        awlen  : output,
        awlock : output,

        wvalid: output,
        wready: input ,
        wlast : output,
        wdata : output,
        wstrb : output,
        wid   : output,

        bvalid: input ,
        bready: output,
        bresp : input ,
        bid   : input ,

        arvalid: output,
        arready: input ,
        araddr : output,
        arsize : output,
        arburst: output,
        arcache: output,
        arprot : output,
        arid   : output,
        arlen  : output,
        arlock : output,

        rvalid: input ,
        rready: output,
        rlast : input ,
        rdata : input ,
        rresp : input ,
        rid   : input ,

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

    }

    modport slave {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

        ..converse(master)
    }

    modport monitor {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

        ..input
    }

    modport write_master {

        awvalid: output,
        awready: input ,
        awaddr : output,
        awsize : output,
        awburst: output,
        awcache: output,
        awprot : output,
        awid   : output,
        awlen  : output,
        awlock : output,

        wvalid: output,
        wready: input ,
        wlast : output,
        wdata : output,
        wstrb : output,
        wid   : output,

        bvalid: input ,
        bready: output,
        bresp : input ,
        bid   : input ,

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,

    }

    modport read_master {

        arvalid: output,
        arready: input ,
        araddr : output,
        arsize : output,
        arburst: output,
        arcache: output,
        arprot : output,
        arid   : output,
        arlen  : output,
        arlock : output,

        rvalid: input ,
        rready: output,
        rlast : input ,
        rdata : input ,
        rresp : input ,
        rid   : input ,

        araddr_ack: import,
        rdata_ack : import,

    }

    modport write_slave {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,

        ..converse(write_master)
    }

    modport read_slave {

        araddr_ack: import,
        rdata_ack : import,

        ..converse(read_master)
    }

}

///### AXI4-Lite bus interface
///
///#### Modports:
///* master - AXI Master IP
///
///* slave - AXI slave IP
///
///* monitor - Debugging - All signals are declared as input
///
///* write_master - Reduced signal count for master IP that only writes to slave
///
///* read_master - Reduced signal count for master IP that only reads from slave
///
///* write_slave - Reduced signal count for slave IP that only receives writes from master
///
///* read_slave - Reduced signal count for slave IP that only replies to read requests from master
///
///#### Convenience functions:
///* awaddr_ack() = awready && awvalid
///
///* wdata_ack() = wready && wvalid
///
///* bresp_ack() = bready && bvalid
///
///* araddr_ack() = arready && arvalid
///
///* rdata_ack() = rready && rvalid
///
///#### Instantiation:
///```
///inst c: axi4_lite_if::< axi4_lite_pkg::< ADDR_W, DATA_W_BYTES, ID_W > >;
///```
///#### Usage in module definition with modport:
///```
///module my_axi4_lite_slave ( aclk: input clock_posedge,
///                            aresetn: input reset_sync_low,
///                            axi: modport axi4_lite_if::<axi4_lite_pkg::<32, 4 ,8>>::slave )
///{
///
///}
///```
///#### Note:
///The awid, arid, bid and rid signals on the slave modports are optional.
///These and are used to connect compatible AXI4-Lite slaves to a full AXI4 interface (AXI ID reflection on slave required).
pub interface axi4_lite_if::<PKG: axi4_lite_prototype> {

    import PKG::*;

    //Write address channel
    var awvalid: logic ;
    var awready: logic ;
    var awaddr : addr_t;
    var awprot : prot_t;
    var awid   : id_t  ;

    //Write data channel
    var wvalid: logic ;
    var wready: logic ;
    var wdata : data_t;
    var wstrb : strb_t;

    //Write response channel
    var bvalid: logic  ;
    var bready: logic  ;
    var bresp : bresp_t;
    var bid   : id_t   ;

    //Read address channel
    var arvalid: logic ;
    var arready: logic ;
    var araddr : addr_t;
    var arprot : prot_t;
    var arid   : id_t  ;

    //Read data channel
    var rvalid: logic  ;
    var rready: logic  ;
    var rdata : data_t ;
    var rresp : rresp_t;
    var rid   : id_t   ;

    function awaddr_ack () -> logic {
        return awready && awvalid;
    }

    function wdata_ack () -> logic {
        return wready && wvalid;
    }

    function bresp_ack () -> logic {
        return bready && bvalid;
    }

    function araddr_ack () -> logic {
        return arready && arvalid;
    }

    function rdata_ack () -> logic {
        return rready && rvalid;
    }

    modport master {

        awvalid: output,
        awready: input ,
        awaddr : output,
        awprot : output,

        wvalid: output,
        wready: input ,
        wdata : output,
        wstrb : output,

        bvalid: input ,
        bready: output,
        bresp : input ,

        arvalid: output,
        arready: input ,
        araddr : output,
        arprot : output,

        rvalid: input ,
        rready: output,
        rdata : input ,
        rresp : input ,

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

    }

    modport slave {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

        awid: input ,
        arid: input ,
        bid : output,
        rid : output,

        ..converse(master)
    }

    modport monitor {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,
        araddr_ack: import,
        rdata_ack : import,

        ..input
    }

    modport write_master {

        awvalid: output,
        awready: input ,
        awaddr : output,
        awprot : output,
        awid   : output,

        wvalid: output,
        wready: input ,
        wdata : output,
        wstrb : output,

        bvalid: input ,
        bready: output,
        bresp : input ,
        bid   : input ,

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,

    }

    modport read_master {

        arvalid: output,
        arready: input ,
        araddr : output,
        arprot : output,
        arid   : output,

        rvalid: input ,
        rready: output,
        rdata : input ,
        rresp : input ,
        rid   : input ,

        araddr_ack: import,
        rdata_ack : import,

    }

    modport write_slave {

        awaddr_ack: import,
        wdata_ack : import,
        bresp_ack : import,

        ..converse(write_master)
    }

    modport read_slave {

        araddr_ack: import,
        rdata_ack : import,

        ..converse(read_master)
    }
}
